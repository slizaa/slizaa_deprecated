grammar org.slizaa.neo4j.hierarchicalgraph.mappingdsl.MappingDsl with org.eclipse.xtext.common.Terminals

generate mappingDsl "http://www.slizaa.org/neo4j/hierarchicalgraph/mappingdsl/MappingDsl"

MappingDescriptor:
	'mapping' qualifiedName=QualifiedName '{'
	metaInformation=MetaInformation
	structureDescriptor=StructureDescriptor?
	visualisationDescriptor=VisualizationDescriptor?
	'}';

MetaInformation:
	{MetaInformation} (('name' name=STRING)? &
	('description' description=STRING)?);

StructureDescriptor:
	{StructureDescriptor} 'structure' '{'
	(topLevelNodeQueries=TopLevelNodesQueries? &
	(nodeHierarchyQueries=NodeHierarchyQueries)? &
	(dependencyQueries=DependencyQueries)?)
	'}';

TopLevelNodesQueries:
	{TopLevelNodesQueries} 'topLevelNodeQueries' '{'
	queries+=CypherQuery? (',' queries+=CypherQuery)*
	'}';

NodeHierarchyQueries:
	{NodeHierarchyQueries} 'nodeHierarchyQueries' '{'
	queries+=CypherQuery? (',' queries+=CypherQuery)*
	'}';

DependencyQueries:
	{DependencyQueries} 'dependencyQueries' '{'
	(simpleDependencyQueries+=SimpleDependencyQueries | aggregatedDependencyQueries+=AggregatedDependencyQuery)*
	'}';

AggregatedDependencyQuery:
	{AggregatedDependencyQuery} 'aggregatedDependencyQuery' '{'
	queries+=CypherQuery (',' queries+=CypherQuery)*
	'onResolve' '{'
	(queries+=CypherQuery)*
	'}'
	'}';

SimpleDependencyQueries:
	{SimpleDependencyQueries} 'simpleDependencyQueries' '{'
	(queries+=CypherQuery)*
	'}';

VisualizationDescriptor:
	{VisualizationDescriptor} 'visualization' '{'
	(nodeVisualizationDefinition+=NodeVisualizationDefinition)*
	'}';

NodeVisualizationDefinition:
	'node' condition=Function '{'
	labelProperties=LabelProperties
	switch=labelSwitch?
	'}';

labelSwitch:
	'on' function=Function '{'
	(labelCases+=LabelCase)+
	'}';

LabelCase:
	case=STRING ':' '{' labelProperties=LabelProperties '}';

LabelProperties:
	{LabelProperties} (('textLabel' textLabel=Expression)? & ('baseImage' baseImage=Expression)? &
	('overlayImageTopLeft' overlayImageTopLeft=Expression)? & ('overlayImageTopRight'
	overlayImageTopRight=Expression)? & ('overlayImageBottomLeft' overlayImageBottomLeft=Expression)? &
	('overlayImageBottomRight' overlayImageBottomRight=Expression)?);

Expression:
	{StringConstant} value=STRING  | Function;

Function:
	name=ID '(' parameters+=Expression (',' parameters+=Expression)* ')';

QualifiedName:
	ID ('.' ID)*;

CypherQuery:
	STRING;


